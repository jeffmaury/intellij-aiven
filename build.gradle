
plugins {
    id "org.jetbrains.intellij" version "1.16.0"
    id "idea"
    id "java"
    id "jacoco"
    id "org.sonarqube" version "4.4.1.3373"
    id "org.openapi.generator" version "7.0.1"
}

repositories {
    mavenLocal()
    maven { url 'https://repository.jboss.org' }
    mavenCentral()
    maven { url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies' }
}

sourceCompatibility = '11'
targetCompatibility = '11'

intellij {
    version = ideaVersion //for a full list of IntelliJ IDEA releases please see https://www.jetbrains.com/intellij-repository/releases
    pluginName = 'Aiven'
    // use '<git repo clone>/build/idea-sandbox/plugins/<plugin>' if working from source
    plugins = [
    ]
	updateSinceUntilBuild = false
}

//with this option enabled, build will fail about IDEA expiration builds
buildSearchableOptions.enabled = false

runPluginVerifier {
    ideVersions = [ideaVersion]
    subsystemsToCheck = 'without-android'
    externalPrefixes = ['org.bouncycastle.jsse', 'org.openjsse', 'org.conscrypt', 'org.jetbrains.yaml', 'io.fabric8', 'com.fasterxml.jackson.dataformat']
}

publishPlugin {
    token = jetBrainsToken
    channels = [jetBrainsChannel]
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

sourceSets {
    integrationTest {
        java.srcDir file('src/it/java')
        resources.srcDir file('src/it/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
    main {
        java.srcDirs += file("aivenapi-java/src/main/java")
    }
    test {
        java.srcDirs += file("aivenapi-java/src/test/java")
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    mustRunAfter test
}

task integrationUITest(type: Test) {
    useJUnitPlatform {
    	includeTags 'ui-test'
    }
    description = 'Runs the integration UI tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { true }
    mustRunAfter test
}

dependencies {
    implementation(
            'com.microsoft.kiota:microsoft-kiota-abstractions:0.8.0',

            'com.microsoft.kiota:microsoft-kiota-serialization-form:0.8.0',
            'com.microsoft.kiota:microsoft-kiota-serialization-json:1.0.0',
            'com.microsoft.kiota:microsoft-kiota-serialization-text:0.8.0',
            'com.microsoft.kiota:microsoft-kiota-serialization-multipart:0.8.0',
            'jakarta.annotation:jakarta.annotation-api:2.1.1')
    implementation('com.microsoft.kiota:microsoft-kiota-http-okHttp:0.8.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    implementation('com.redhat.devtools.intellij:intellij-common:1.9.3') {
        exclude group: 'io.fabric8'
    }
    testImplementation(
            "org.junit.jupiter:junit-jupiter-engine:${junitVersion}",
            "org.junit.jupiter:junit-jupiter-api:${junitVersion}",
            "org.junit.jupiter:junit-jupiter:${junitVersion}")
}

sonarqube {
    properties {
        property "sonar.projectKey", ""
        property "sonar.organization", ""
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.junit.reportsPath", "$buildDir/test-results/"
    }
}

jacocoTestReport {
    getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
    reports {
        xml.required = true
    }
}

runIde {
}

group 'com.jeffmaury.aiven.intellij.jboss.tools.intellij'
version projectVersion // Plugin version
